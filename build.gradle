plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.2'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.graalvm.buildtools.native' version '0.9.18'
    id 'com.github.jmongard.git-semver-plugin'
    id 'com.adarshr.test-logger'
    id 'com.google.cloud.tools.jib' version '3.2.0'
}

def publishToDocker = Boolean.parseBoolean(System.getenv().getOrDefault('PUBLISH_TO_DOCKER', 'false'))
def dockerUsername = System.getenv().getOrDefault('DOCKER_USERNAME', 'user')
def dockerPassword = System.getenv().getOrDefault('DOCKER_PASSWORD', 'pass')
def gitCommit = System.getenv().getOrDefault('GIT_COMMIT', 'commit')

group = 'biz.digissance'
version = semver.version as String
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
//    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-json'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'net.liccioni.archetypes:party:0.0.45'
//    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'org.postgresql:postgresql'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

bootBuildImage {
    imageName = "ghcr.io/digissance/graalvm-demo"
    publish = publishToDocker
    tags = ["ghcr.io/digissance/graalvm-demo:${semver.version}"]
    docker {
        publishRegistry {
            username = dockerUsername
            password = dockerPassword
        }
    }
}

jib {
    to {
        image = "ghcr.io/digissance/graalvm-demo"
        tags = [semver.version, gitCommit] as List<String>
    }
}